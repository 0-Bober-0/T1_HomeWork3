plugins {
	id 'java-library'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
}

group = 'ru.t1.daev'
version = '1.0.0'
archivesBaseName = 'bishop-starter'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24) // Изменено с 24 на 17 (Spring Boot 3 требует Java 17)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'io.micrometer:micrometer-core'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.kafka:spring-kafka'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
	archiveClassifier = ''
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifactId = 'bishop-starter'
			// Явно указываем версии для всех зависимостей
			versionMapping {
				usage('java-api') {
					fromResolutionOf('runtimeClasspath')
				}
				usage('java-runtime') {
					fromResolutionResult()
				}
			}
		}
	}
}

// Решение проблемы с зависимостями без версий
tasks.withType(GenerateModuleMetadata) {
	enabled = false
}